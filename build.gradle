buildscript {
	repositories {
		mavenCentral()
		mavenLocal()
		maven { url 'http://dl.bintray.com/jetbrains/spek' }
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
		classpath "org.junit.platform:junit-platform-gradle-plugin:$junitPlatformVersion"
		classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$usedDetektGradleVersion"
	}
}

plugins {
	id 'com.jfrog.bintray' version '1.7.3'
	id 'com.github.ben-manes.versions' version '0.15.0'
	id 'com.github.johnrengelman.shadow' version '1.2.3'
	id 'org.sonarqube' version '2.5'
}

apply plugin: 'io.gitlab.arturbosch.detekt'

allprojects {

	group 'io.gitlab.arturbosch.detekt'
	version detektVersion

	repositories {
		mavenCentral()
		mavenLocal()
		maven { url 'http://dl.bintray.com/jetbrains/spek' }
	}

	apply plugin: 'org.junit.platform.gradle.plugin'
	apply plugin: 'kotlin'
	apply plugin: 'maven'
	apply plugin: 'maven-publish'
	apply plugin: 'com.jfrog.bintray'

	def projectName = it.name
	if (projectName == 'detekt-cli' || projectName == 'detekt-watch-service') {
		apply plugin: 'application'
		apply plugin: 'com.github.johnrengelman.shadow'
	}

	compileKotlin {
		kotlinOptions.jvmTarget = "1.8"
		kotlinOptions.freeCompilerArgs = ["-Xskip-runtime-version-check"]

	}
	compileTestKotlin {
		kotlinOptions.jvmTarget = "1.8"
		kotlinOptions.freeCompilerArgs = ["-Xskip-runtime-version-check"]
	}

	junitPlatform {
		filters {
			engines {
				include 'spek', 'junit-jupiter'
			}
		}
	}

	uploadArchives {
		repositories {
			mavenDeployer {
				repository(url: uri('../repo'))
			}
		}
	}

	bintray {
		user = System.getenv('BINTRAY_USER') ?: ''
		key = System.getenv('BINTRAY_API_KEY') ?: ''
		publications = ['DetektPublication']

		pkg {
			repo = 'code-analysis'
			name = 'detekt'
			userOrg = 'arturbosch'
			licenses = ['Apache-2.0']
			vcsUrl = 'https://github.com/arturbosch/detekt'

			version {
				name = project.version
				released = new Date()
			}
		}
	}

	task sourcesJar(type: Jar, dependsOn: classes) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	artifacts {
		archives sourcesJar
	}

	def pomConfig = {
		licenses {
			license {
				name 'The Apache Software License, Version 2.0'
				url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
				distribution 'repo'
			}
		}
		developers {
			developer {
				id 'Artur Bosch'
				name 'Artur Bosch'
				email 'arturbosch@gmx.de'
			}
		}

		scm {
			url 'https://github.com/arturbosch/detekt'
		}
	}

	publishing {
		publications {
			DetektPublication(MavenPublication) {
				from components.java
				artifact sourcesJar
				groupId project.group
				artifactId project.name
				version project.version
				pom.withXml {
					def root = asNode()
					root.appendNode('description', 'Static code analysis for Kotlin')
					root.appendNode('name', 'detekt')
					root.appendNode('url', 'https://github.com/arturbosch/detekt')
					root.children().last() + pomConfig
				}
			}
		}
	}

	configurations {
		kotlinImplementation
		kotlinTest
		junitPlatform
	}

	dependencies {
		kotlinImplementation "org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlinVersion"
		kotlinImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
		kotlinTest "org.junit.jupiter:junit-jupiter-api:$junitEngineVersion"
		kotlinTest "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
		kotlinTest "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
		kotlinTest "org.assertj:assertj-core:$assertjVersion"
		kotlinTest "org.jetbrains.spek:spek-api:$spekVersion"
		kotlinTest "org.jetbrains.spek:spek-subject-extension:$spekVersion"
		kotlinTest "org.junit.jupiter:junit-jupiter-engine:$junitEngineVersion"
		junitPlatform "org.junit.platform:junit-platform-launcher:$junitPlatformVersion"
		junitPlatform "org.junit.platform:junit-platform-console:$junitPlatformVersion"
		junitPlatform "org.jetbrains.spek:spek-junit-platform-engine:$spekVersion"
	}

	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
	}
}

String USER_HOME = System.getProperty('user.home')

detekt {
	debug = true
	version = usedDetektVersion
	profile = 'failfast'

	profile('main') {
		input = projectDir
		filters = '.*/test/.*, .*/resources/.*, .*/build/.*'
		config = "$projectDir/detekt-cli/src/main/resources/default-detekt-config.yml"
		baseline = "$projectDir/reports/baseline.xml"
	}

	profile('failfast') {
		input = projectDir
		config = "$projectDir/reports/failfast.yml"
	}

	idea {
		path = "$USER_HOME/.idea"
		codeStyleScheme = "$USER_HOME/.idea/idea-code-style.xml"
		inspectionsProfile = "$USER_HOME/.idea/inspect.xml"
		report = "$project.projectDir/reports"
		mask = '*.kt,'
	}
}
